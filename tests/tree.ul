type TreeKey string;
type TreeValue string;

compound_type TreeNode #Node (key:TreeKey,
                              value:TreeValue,
                              left:TreeNode,
                              right:TreeNode),
                       #Terminator;

compound_type TreeLookup #Valid TreeValue,
                         #NotFound;

type Tree (root: TreeNode,
           add: (void)(TreeKey, TreeValue),
           get: (TreeLookup)(TreeKey),
           size: (int)());

type TreeConstructor (Tree)();

TreeConstructor createTree =
(Tree)() {
    return (
        root: #Terminator,

        add: (void)(TreeKey key, TreeValue value) {

            (TreeNode)(TreeNode,TreeKey,TreeValue) hiddenAdd =
            (TreeNode)(TreeNode node, TreeKey key, TreeValue value) {
                unpack node as #Terminator {
                    return (
                            key: key,
                            value: value,
                            left: #Terminator,
                            right: #Terminator
                        );
                }

                unpack node as #Node n {
                    if (key >= n.key) {
                        n.right = hiddenAdd(n.right, key, value);
                    }
                    if (key < n.key) {
                        n.left = hiddenAdd(n.left, key, value);
                    }
                    return n;
                }
            };

            root = hiddenAdd(root, key, value);
        },

        get: (TreeLookup)(TreeKey key) {

            (TreeLookup)(TreeNode,TreeKey) hiddenGet =
            (TreeLookup)(TreeNode node, TreeKey key) {
                unpack node as #Node n {
                    if (key > n.key) {
                        return hiddenGet(n.right, key);
                    }
                    if (key < n.key) {
                        return hiddenGet(n.left, key);
                    }
                    return n.value;
                }

                unpack node as #Terminator {
                    return #NotFound;
                }
            };

            return hiddenGet(root, key);
        },

        size: (int)() {

            (int)(TreeNode) hiddenSize =
            (int)(TreeNode node) {
                unpack node as #Terminator {
                    return 0;
                }

                unpack node as #Node n {
                    int a = hiddenSize(n.left);
                    int b = hiddenSize(n.right);
                    return 1 + a + b;
                }
            };

            return hiddenSize(root);
        }
    );
};

(void)(Tree,TreeKey) lookupTest =
(void)(Tree tree, TreeKey key) {
    TreeLookup result = tree.get(key);
    unpack result as #Valid value {
        print(key + ": " + value);
    }
    unpack result as #NotFound {
        print(key + ": Not Found");
    }
};

Tree testTree = createTree();
testTree.add("emil", "hernvall");
testTree.add("daniel", "andersson");
testTree.add("miki", "momen");
testTree.add("hannes", "hernvall");
testTree.add("erik", "ödmark");
testTree.add("torbjörn", "hernvall");

print("size: " + intToStr(testTree.size()));

lookupTest(testTree, "erik");
lookupTest(testTree, "miki");
lookupTest(testTree, "emil");
lookupTest(testTree, "felicia");
