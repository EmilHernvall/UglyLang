options {
    STATIC = false ;
    UNICODE_INPUT = true ;
}

PARSER_BEGIN(Parser)
package se.c0la.uglylang;

import java.util.*;

import se.c0la.uglylang.ast.*;
import se.c0la.uglylang.type.*;

public class Parser
{
    public enum Pass {
        FIRST,
        SECOND;
    };

    private Map<String, Type> types = null;
    private Map<String, CompoundType> subTypeLookup = null;
    private List<String> imports;
    private Map<String, Module> deps;

    private Pass pass = Pass.FIRST;

    public void setPass(Pass pass)
    {
        this.pass = pass;
    }

    public Map<String, Type> getTypes()
    {
        return types;
    }

    public List<String> getImports()
    {
        return imports;
    }

    public void setDependencies(Map<String, Module> deps)
    {
        this.deps = deps;
    }
}
PARSER_END(Parser)

SKIP : { "/*" : WithinCStyle }
SKIP : { "//" : WithinCPPStyle }
<WithinCStyle> SKIP : { "*/" : DEFAULT }
<WithinCPPStyle> SKIP : { "\n" : DEFAULT }
<WithinCStyle> MORE : { <~[]> }
<WithinCPPStyle> MORE : { <~[]> }

TOKEN : { < #ident : <nmstart> (<nmchar>)* > }
TOKEN : { < #typename : "#" <nmstart> (<nmchar>)* > }
TOKEN : { < #name : (<nmchar>)+ > }
TOKEN : { < #nmstart : ["a"-"z","A"-"Z"] | "_" | <nonascii> | <escape> > }
TOKEN : { < #nonascii : ["\u0080"-"\uD7FF","\uE000"-"\uFFFD"] > }
TOKEN : { < #unicode : "\\" (["a"-"f","A"-"F","0"-"9"]){1,6} (<wc>)? > }
TOKEN : { < #escape : <unicode> | "\\" ["\u0020"-"\u007E","\u0080"-"\uD7FF","\uE000"-"\uFFFD"] > }
TOKEN : { < #nmchar : ["a"-"z","A"-"Z","0"-"9"] | "_" | <nonascii> | <escape> > }
TOKEN : { < #num : (["0"-"9"])+ | (["0"-"9"])* "." (["0"-"9"])+ > }
TOKEN : { < #string : "\"" (<stringchar> | "'")* "\"" | "'" (<urlchar> | "\"")* "'" > }
TOKEN : { < #stringchar : <urlchar> | "\u0020" | "\\" <nl> > }
TOKEN : { < #urlchar : ["\u0009","\u0021","\u0023"-"\u0026","\u0028"-"\u007E"] | <nonascii> | <escape> > }
TOKEN : { < #nl : "\n" | "\r" "\n" | "\r" | "\u000C" > }
TOKEN : { < #wc: "\u0009" | "\n" | "\u000C" | "\r" | "\u0020" > }
TOKEN : { < #w : (<wc>)* > }

TOKEN : { < IMPORT : "import" > }
TOKEN : { < EXPORT : "export" > }
TOKEN : { < UNPACK : "unpack" > }
TOKEN : { < AS : "as" > }
TOKEN : { < TYPEDECL : "type" > }
TOKEN : { < COMPOUNDTYPEDECL : "compound_type" > }
TOKEN : { < SUBSCRIPT : "." > }
TOKEN : { < AND : "and" > }
TOKEN : { < OR : "or" > }
TOKEN : { < NOT : "not" > }
TOKEN : { < XOR : "xor" > }
TOKEN : { < IFSTMT : "if" > }
TOKEN : { < ELSEIFSTMT : "elif" > }
TOKEN : { < ELSESTMT : "else" > }
TOKEN : { < WHILESTMT : "while" > }
TOKEN : { < RETURNSTMT : "return" > }
TOKEN : { < EQ : "==" > }
TOKEN : { < NOTEQ : "!=" > }
TOKEN : { < GT : ">" > }
TOKEN : { < GTEQ : ">=" > }
TOKEN : { < LT : "<" > }
TOKEN : { < LTEQ : "<=" > }
TOKEN : { < ASSIGN : "=" > }
TOKEN : { < ADD : "+" > }
TOKEN : { < SUB : "-" > }
TOKEN : { < MUL : "*" > }
TOKEN : { < DIV : "/" > }
TOKEN : { < MOD : "%" > }
TOKEN : { < EXP : "^" > }
TOKEN : { < LPAR : "(" > }
TOKEN : { < RPAR : ")" > }
TOKEN : { < LCLAUSE : "{" > }
TOKEN : { < RCLAUSE : "}" > }
TOKEN : { < LBRACKET : "[" > }
TOKEN : { < RBRACKET : "]" > }
TOKEN : { < STMTBREAK : ";" > }
TOKEN : { < LISTSEP : "," > }
TOKEN : { < KEYVALUESEP : ":" > }
TOKEN : { < TYPENAME : <typename> > }
TOKEN : { < IDENT : <ident> > }
TOKEN : { < STRING : <string> > }
TOKEN : { < NUMBER : <num> > }
TOKEN : { < WHITESPACE : (<wc>)+ > }

/*
 * rule for whitespace:
 * a rule consumes all the trailing whitespace, but no
 * whitespace ahead.
 */

List<Node> parse() :
{
    Token t, begin ;
    Node n ;
    List<Node> nodes ;
}
{
    {
        nodes = new ArrayList<Node>();

        types = new HashMap<String, Type>();
        types.put("int", new IntegerType());
        types.put("string", new StringType());
        types.put("bool", new BooleanType());
        types.put("void", new VoidType());

        subTypeLookup = new HashMap<String, CompoundType>();

        imports = new ArrayList<String>();
    }
    (<WHITESPACE>)*
    (
        LOOKAHEAD( <TYPEDECL> )
            n = TypeDeclaration()
            { nodes.add(n); }
        |
        LOOKAHEAD( <COMPOUNDTYPEDECL> )
            n = CompoundTypeDeclaration()
            { nodes.add(n); }
        |
        LOOKAHEAD( <IMPORT> )
            begin = <IMPORT>
            (<WHITESPACE>)*
            t = <IDENT>
            <STMTBREAK>
            (<WHITESPACE>)*
            {
                n = new ImportNode(t.image);
                n.setPosition(begin.beginLine, begin.beginColumn);
                nodes.add(n);
                if (pass == Pass.FIRST) {
                    imports.add(t.image);
                } else {
                    // copy types from dependency
                    Module mod = deps.get(t.image);
                    types.putAll(mod.getTypes());
                }
            }
        |
        LOOKAHEAD( <EXPORT> )
            begin = <EXPORT>
            (<WHITESPACE>)*
            t = <IDENT>
            <STMTBREAK>
            (<WHITESPACE>)*
            {
            n = new ExportNode(t.image);
            n.setPosition(begin.beginLine, begin.beginColumn);
            nodes.add(n);
            }
        |
            n = Statement()
            { nodes.add(n); }
    )*

    { return nodes; }
}

Type FunctionType() :
{
    Type type, type2 ;
    List<Type> params ;
}
{
    { params = new ArrayList<Type>(); }
    <LPAR>
    type = Type()
    <RPAR>
    <LPAR>
    [
        type2 = Type()
        { params.add(type2); }
        (
            <LISTSEP>
            (<WHITESPACE>)*
            type2 = Type()
            { params.add(type2); }
        )*
    ]
    <RPAR>
    { type = new FunctionType(type, params); }

    { return type; }
}

Type ObjectType() :
{
    Token t ;
    Type type, type2 ;
    Map<String, Type> params ;
}
{
    { params = new LinkedHashMap<String, Type>(); }
    <LPAR>
    [
        t = <IDENT>
        <KEYVALUESEP>
        (<WHITESPACE>)*
        type2 = Type()
        { params.put(t.image, type2); }
        (
            <LISTSEP>
            (<WHITESPACE>)*
            t = <IDENT>
            <KEYVALUESEP>
            (<WHITESPACE>)*
            type2 = Type()
            { params.put(t.image, type2); }
        )*
    ]
    <RPAR>
    { type = new ObjectType(params); }

    { return type; }
}

Type Type() :
{
    Token t ;
    Type type, type2 ;
    List<Type> params ;
}
{
    (
        LOOKAHEAD ( FunctionType() )
        type = FunctionType()
        |
        LOOKAHEAD ( ObjectType() )
        type = ObjectType()
        |
        (
            t = <IDENT>
            {
                type = types.get(t.image);
                if (type == null && pass == Pass.SECOND) {
                    throw new ParseException("Type " + t.image + " not found.");
                }
            }
        )
    )
    (
        <LBRACKET>
        <RBRACKET>
        { type = new ArrayType(type); }
    )*
    (<WHITESPACE>)*

    { return type; }
}

Node TypeDeclaration() :
{
    Token t, begin ;
    Type type ;
    Node n, n2 ;
}
{
    begin = <TYPEDECL>
    (<WHITESPACE>)*
    t = <IDENT>
    (<WHITESPACE>)*
    type = Type()
    <STMTBREAK>
    (<WHITESPACE>)*
    {
    types.put(t.image, type);
    n = new TypeDeclNode(type, t.image);
    n.setPosition(begin.beginLine, begin.beginColumn);
    return n;
    }
}

Node CompoundTypeDeclaration() :
{
    String name ;
    Token t, begin ;
    Type type = null ;
    CompoundType compoundType ;
    Node n, n2 ;
    Map<String, Type> subTypes ;
}
{
    {
    subTypes = new HashMap<String, Type>();
    }

    begin = <COMPOUNDTYPEDECL>
    (<WHITESPACE>)*
    t = <IDENT>
    {
    name = t.image;
    compoundType = new CompoundType(name, subTypes);
    types.put(name, compoundType);
    }
    (<WHITESPACE>)*

    t = <TYPENAME>
    (<WHITESPACE>)*
    [ type = Type() ]
    {
    if (type == null) {
        type = new CompoundTerminalType(t.image);
    }
    subTypes.put(t.image, type);
    subTypeLookup.put(t.image, compoundType);
    type = null;
    }

    (
        <LISTSEP>
        (<WHITESPACE>)*
        t = <TYPENAME>
        (<WHITESPACE>)*
        [ type = Type() ]
        {
        if (type == null) {
            type = new CompoundTerminalType(t.image);
        }
        if (subTypeLookup.containsKey(t.image)) {
            throw new ParseException(t.image + " has already been declared.");
        }
        subTypeLookup.put(t.image, compoundType);
        subTypes.put(t.image, type);
        type = null;
        }
    )*

    <STMTBREAK>
    (<WHITESPACE>)*
    {
    n = new TypeDeclNode(compoundType, name);
    n.setPosition(begin.beginLine, begin.beginColumn);
    return n;
    }
}

Node Statement() :
{
    Token t, begin ;
    Node n, n2 ;
    Expression expr, expr2 ;
    Variable var ;
    Declaration d ;
}
{
    (
        LOOKAHEAD( <IFSTMT> )
            n = IfStatement()
        |
        LOOKAHEAD( <UNPACK> )
            n = UnpackStatement()
        |
        LOOKAHEAD( <WHILESTMT> )
            n = WhileStatement()
        |
        LOOKAHEAD( <RETURNSTMT> )
            t = <RETURNSTMT>
            (<WHITESPACE>)+
            expr = Expression()
            <STMTBREAK>
            (<WHITESPACE>)*
            {
            n = new ReturnStatement(expr);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        |
        LOOKAHEAD( Declaration() <ASSIGN> )
            d = Declaration()
            t = <ASSIGN>
            (<WHITESPACE>)*
            expr = Expression()
            {
            n = new AssignDeclarationNode(d, expr);
            n.setPosition(t.beginLine, t.beginColumn);
            }
            <STMTBREAK>
            (<WHITESPACE>)*
        |
            n = Expression()
            <STMTBREAK>
            (<WHITESPACE>)*
    )

    { return n; }
}

Node UnpackStatement() :
{
    Token t, begin ;
    Expression src ;
    String dst = null ;
    Node n ;
    String subType ;
    List<Node> stmts;
}
{
    { stmts = new ArrayList<Node>(); }
    begin = <UNPACK>
    (<WHITESPACE>)*

    src = Expression()

    <AS>
    (<WHITESPACE>)*

    t = <TYPENAME>
    (<WHITESPACE>)*
    { subType = t.image; }

    [
        t = <IDENT>
        (<WHITESPACE>)*
        { dst = t.image; }
    ]

    <LCLAUSE>
    (<WHITESPACE>)*
    (
        n = Statement()
        { stmts.add(n); }
    )*
    <RCLAUSE>
    (<WHITESPACE>)*

     {
         CompoundType type = subTypeLookup.get(subType);
         if (type == null && pass == Pass.SECOND) {
            throw new ParseException(subType + " not found.");
         }
         n = new UnpackStatement(src, dst, type, subType, stmts);
         n.setPosition(begin.beginLine, begin.beginColumn);
         return n;
     }
}

ElseIfStatement ElseIfStatement() :
{
    Token t, begin ;
    Node n, n2 ;
    List<Node> stmts;
}
{
    { stmts = new ArrayList<Node>(); }
    begin = <ELSEIFSTMT>
    (<WHITESPACE>)*
    n = Expression()
    <LCLAUSE>
    (<WHITESPACE>)*
    (
        n2 = Statement()
        { stmts.add(n2); }
    )*
    <RCLAUSE>
    (<WHITESPACE>)*

     {
     ElseIfStatement stmt = new ElseIfStatement(n, stmts);
     stmt.setPosition(begin.beginLine, begin.beginColumn);
     return stmt;
     }
}

ElseStatement ElseStatement() :
{
    Token t, begin ;
    Node n, n2 ;
    List<Node> stmts;
}
{
    { stmts = new ArrayList<Node>(); }
    begin = <ELSESTMT>
    (<WHITESPACE>)*
    <LCLAUSE>
    (<WHITESPACE>)*
    (
        n2 = Statement()
        { stmts.add(n2); }
    )*
    <RCLAUSE>
    (<WHITESPACE>)*

     {
     ElseStatement stmt =  new ElseStatement(stmts);
     stmt.setPosition(begin.beginLine, begin.beginColumn);
     return stmt;
     }
}

Node IfStatement() :
{
    Token t, begin ;
    Node n, n2 ;
    ElseIfStatement elseIfStmt = null ;
    List<ElseIfStatement> elseIfList ;
    ElseStatement elseStmt = null ;
    List<Node> stmts;
}
{
    { stmts = new ArrayList<Node>(); }
    { elseIfList = new ArrayList<ElseIfStatement>(); }
    begin = <IFSTMT>
    (<WHITESPACE>)*
    n = Expression()
    <LCLAUSE>
    (<WHITESPACE>)*
    (
        n2 = Statement()
        { stmts.add(n2); }
    )*
    <RCLAUSE>
    (<WHITESPACE>)*
    (
        elseIfStmt = ElseIfStatement()
        { elseIfList.add(elseIfStmt); }
    )*
    [ elseStmt = ElseStatement() ]

     {
     n = new IfStatement(n, stmts, elseIfList, elseStmt);
     n.setPosition(begin.beginLine, begin.beginColumn);
     return n;
     }
}

Node WhileStatement() :
{
    Token t, begin ;
    Node n, n2 ;
    List<Node> stmts;
}
{
    { stmts = new ArrayList<Node>(); }
    begin = <WHILESTMT>
    (<WHITESPACE>)*
    n = Expression()
    <LCLAUSE>
    (<WHITESPACE>)*
    (
        n2 = Statement()
        { stmts.add(n2); }
    )*
    <RCLAUSE>
    (<WHITESPACE>)*

    {
    n = new WhileStatement(n, stmts);
    n.setPosition(begin.beginLine, begin.beginColumn);
    return n;
    }
}

Declaration Declaration() :
{
    Token t ;
    Type type ;
    String name ;
    Node value ;
    Declaration d ;
}
{
    type = Type()
    t = <IDENT>
    { name = t.image; }
    { d = new Declaration(type, name); }
    (<WHITESPACE>)*

    {
    d.setPosition(t.beginLine, t.beginColumn);
    return d;
    }
}

ObjectNode Object() :
{
    Map<String, Expression> nodes ;
    Token t, begin ;
    Expression n ;
}
{
    { nodes = new LinkedHashMap<String, Expression>(); }
    begin = <LPAR>
    (<WHITESPACE>)*
    t = <IDENT>
    <KEYVALUESEP>
    (<WHITESPACE>)*
    n = Expression()
    { nodes.put(t.image, n); }
    (
        <LISTSEP>
        (<WHITESPACE>)*
        t = <IDENT>
        <KEYVALUESEP>
        (<WHITESPACE>)*
        n = Expression()
        { nodes.put(t.image, n); }
    )*
    <RPAR>
    (<WHITESPACE>)*

    {
    ObjectNode obj = new ObjectNode(nodes);
    obj.setPosition(begin.beginLine, begin.beginColumn);
    return obj;
    }
}

ArrayNode Array() :
{
    Token begin ;
    List<Expression> nodes ;
    Expression n ;
}
{
    { nodes = new ArrayList<Expression>(); }
    begin = <LBRACKET>
    (<WHITESPACE>)*
    n = Expression()
    { nodes.add(n); }
    (
        <LISTSEP>
        (<WHITESPACE>)*
        n = Expression()
        { nodes.add(n); }
    )*
    <RBRACKET>
    (<WHITESPACE>)*

    {
    ArrayNode stmt = new ArrayNode(nodes);
    stmt.setPosition(begin.beginLine, begin.beginColumn);
    return stmt;
    }
}

FunctionDecl Function() :
{
    Token t, begin ;
    Type type ;
    Node n ;
    Declaration d ;
    FunctionDecl f ;
    List<Declaration> params ;
    List<Node> statements ;
}
{
    { params = new ArrayList<Declaration>(); }
    { statements = new ArrayList<Node>(); }
    begin = <LPAR>
    type = Type()
    <RPAR>
    <LPAR>
    [
        d = Declaration()
        { params.add(d); }
        (
            <LISTSEP>
            (<WHITESPACE>)*
            d = Declaration()
            { params.add(d); }
        )*
    ]
    <RPAR>
    (<WHITESPACE>)*
    <LCLAUSE>
        (<WHITESPACE>)*
        (
            n = Statement()
            { statements.add(n); }
        )*
    <RCLAUSE>
    (<WHITESPACE>)*

    {
    f = new FunctionDecl(type, params, statements);
    f.setPosition(begin.beginLine, begin.beginColumn);
    return f;
    }
}

Expression Expression() :
{
    Token t ;
    Expression n, n2 ;
}
{
    n = ExpressionLogic()
    (
            t = <ASSIGN>
            (<WHITESPACE>)*
            n2 = ExpressionLogic()
            {
            n = new AssignNode(n, n2);
            n.setPosition(t.beginLine, t.beginColumn);
            }
    )*

    { return n; }
}

Expression ExpressionLogic() :
{
    Token t ;
    Expression n, n2 ;
}
{
    n = ExpressionCmp()
    (
            t = <AND>
            (<WHITESPACE>)*
            n2 = ExpressionCmp()
            {
            n = new AndNode(n, n2);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        |
            t = <OR>
            (<WHITESPACE>)*
            n2 = ExpressionCmp()
            {
            n = new OrNode(n, n2);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        |
            t = <XOR>
            (<WHITESPACE>)*
            n2 = ExpressionCmp()
            {
            n = new XorNode(n, n2);
            n.setPosition(t.beginLine, t.beginColumn);
            }
    )*

    { return n; }
}

Expression ExpressionCmp() :
{
    Token t ;
    Expression n, n2 ;
}
{
    n = ExpressionArithmetic()
    (
            t = <EQ>
            (<WHITESPACE>)*
            n2 = ExpressionArithmetic()
            {
            n = new EqualNode(n, n2);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        |
            t = <NOTEQ>
            (<WHITESPACE>)*
            n2 = ExpressionArithmetic()
            {
            n = new NotEqualNode(n, n2);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        |
            t = <LT>
            (<WHITESPACE>)*
            n2 = ExpressionArithmetic()
            {
            n = new LtNode(n, n2);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        |
            t = <GT>
            (<WHITESPACE>)*
            n2 = ExpressionArithmetic()
            {
            n = new GtNode(n, n2);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        |
            t = <LTEQ>
            (<WHITESPACE>)*
            n2 = ExpressionArithmetic()
            {
            n = new LtEqNode(n, n2);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        |
            t = <GTEQ>
            (<WHITESPACE>)*
            n2 = ExpressionArithmetic()
            {
            n = new GtEqNode(n, n2);
            n.setPosition(t.beginLine, t.beginColumn);
            }
    )*

    { return n; }
}

Expression ExpressionArithmetic() :
{
    Token t ;
    Expression n, n2 ;
}
{
    (
        n = ExpressionArithmetic2()
        (
                t = <ADD>
                (<WHITESPACE>)*
                n2 = ExpressionArithmetic2()
                {
                n = new AddNode(n, n2);
                n.setPosition(t.beginLine, t.beginColumn);
                }
            |
                t = <SUB>
                (<WHITESPACE>)*
                n2 = ExpressionArithmetic2()
                {
                n = new SubNode(n, n2);
                n.setPosition(t.beginLine, t.beginColumn);
                }
        )*
    )

    { return n; }
}

Expression ExpressionArithmetic2() :
{
    Token t ;
    Expression n, n2 ;
}
{
    (
        n = ExpressionOperands()
        (
                t = <MUL>
                (<WHITESPACE>)*
                n2 = ExpressionOperands()
                {
                n = new MulNode(n, n2);
                n.setPosition(t.beginLine, t.beginColumn);
                }
            |
                t = <DIV>
                (<WHITESPACE>)*
                n2 = ExpressionOperands()
                {
                n = new DivNode(n, n2);
                n.setPosition(t.beginLine, t.beginColumn);
                }
            |
                t = <MOD>
                (<WHITESPACE>)*
                n2 = ExpressionOperands()
                {
                n = new ModNode(n, n2);
                n.setPosition(t.beginLine, t.beginColumn);
                }
        )*
    )

    { return n; }
}

Expression ExpressionOperands() :
{
    Token t, t2 ;
    Expression n, n2 ;
    List<Expression> params ;
    int seq = 0 ;
}
{
    (
        LOOKAHEAD(
            <LPAR>
            (<WHITESPACE>)*
            <IDENT>
            (<WHITESPACE>)*
            <RPAR>
            (<WHITESPACE>)*
            <LPAR> )
            n = Function()
        |
        LOOKAHEAD(
            <LPAR>
            (<WHITESPACE>)*
            <IDENT>
            (<WHITESPACE>)*
            <KEYVALUESEP> )
            n = Object()
        |
        LOOKAHEAD( <LBRACKET> )
            n = Array()
        |
            <LPAR>
            (<WHITESPACE>)*
            n = Expression()
            <RPAR>
            (<WHITESPACE>)*
        |
            t = <IDENT>
            (<WHITESPACE>)*
            {
            n = new Variable(t.image, seq++);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        |
            t = <NUMBER>
            (<WHITESPACE>)*
            {
            n = new IntegerConstant(t.image);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        |
            t = <TYPENAME>
            (<WHITESPACE>)*
            {
            CompoundType type = subTypeLookup.get(t.image);
            if (type == null && pass == Pass.SECOND) {
                throw new ParseException(t.image + " not found.");
            }
            n = new TypeValue(type, t.image);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        |
            t = <STRING>
            (<WHITESPACE>)*
            {
            n = new StringConstant(t.image.substring(1, t.image.length()-1));
            n.setPosition(t.beginLine, t.beginColumn);
            }
    )

    (
        (
            <SUBSCRIPT>
            t = <IDENT>
            (<WHITESPACE>)*
            {
            n = new SubscriptNode(n, t.image, seq++);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        )
        |
        (
            t = <LBRACKET>
            n2 = Expression()
            <RBRACKET>
            (<WHITESPACE>)*
            {
            n = new IndexNode(n, n2, seq++);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        )
        |
        (
            t = <LPAR>
            (<WHITESPACE>)*
            { params = new ArrayList<Expression>(); }

            [
                n2 = Expression()
                { params.add(n2); }
                (
                    <LISTSEP>
                    (<WHITESPACE>)*
                    n2 = Expression()
                    { params.add(n2); }
                )*
            ]
            <RPAR>
            (<WHITESPACE>)*
            {
            n = new FunctionCall(n, params);
            n.setPosition(t.beginLine, t.beginColumn);
            }
        )
    )*

    { return n; }
}
