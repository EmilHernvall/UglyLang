options {
    STATIC = false ;
    UNICODE_INPUT = true ;
}

PARSER_BEGIN(Parser)
package se.c0la.uglylang;

import java.util.*;

import se.c0la.uglylang.ast.*;
import se.c0la.uglylang.type.*;

public class Parser
{
    public static void main( String[] args )
    throws ParseException, TokenMgrError {
        Parser parser = new Parser(System.in);
        List<Node> nodes = parser.parse();

        CodeGenerationVisitor visitor = new CodeGenerationVisitor();
        for (Node node : nodes) {
            node.accept(visitor);
        }

        visitor.dump();
    }
}
PARSER_END(Parser)

SKIP :
{
  "/*" : WithinComment
}
<WithinComment> SKIP :
{
  "*/" : DEFAULT
}
<WithinComment> MORE :
{
  <~[]>
}

TOKEN : { < #ident : ("-")? <nmstart> (<nmchar>)* > }
TOKEN : { < #name : (<nmchar>)+ > }
TOKEN : { < #nmstart : ["a"-"z","A"-"Z"] | "_" | <nonascii> | <escape> > }
TOKEN : { < #nonascii : ["\u0080"-"\uD7FF","\uE000"-"\uFFFD"] > }
TOKEN : { < #unicode : "\\" (["a"-"f","A"-"F","0"-"9"]){1,6} (<wc>)? > }
TOKEN : { < #escape : <unicode> | "\\" ["\u0020"-"\u007E","\u0080"-"\uD7FF","\uE000"-"\uFFFD"] > }
TOKEN : { < #nmchar : ["a"-"z","A"-"Z","0"-"9"] | "_" | <nonascii> | <escape> > }
TOKEN : { < #num : (["0"-"9"])+ | (["0"-"9"])* "." (["0"-"9"])+ > }
TOKEN : { < #string : "\"" (<stringchar> | "'")* "\"" | "'" (<urlchar> | "\"")* "'" > }
TOKEN : { < #stringchar : <urlchar> | "\u0020" | "\\" <nl> > }
TOKEN : { < #urlchar : ["\u0009","\u0021","\u0023"-"\u0026","\u0028"-"\u007E"] | <nonascii> | <escape> > }
TOKEN : { < #nl : "\n" | "\r" "\n" | "\r" | "\u000C" > }
TOKEN : { < #wc: "\u0009" | "\n" | "\u000C" | "\r" | "\u0020" > }
TOKEN : { < #w : (<wc>)* > }

TOKEN : { < SUBSCRIPT : "." > }
TOKEN : { < AND : "and" > }
TOKEN : { < OR : "or" > }
TOKEN : { < NOT : "not" > }
TOKEN : { < XOR : "xor" > }
TOKEN : { < IFSTMT : "if" > }
TOKEN : { < RETURNSTMT : "return" > }
TOKEN : { < EQ : "==" > }
TOKEN : { < NOTEQ : "!=" > }
TOKEN : { < GT : ">" > }
TOKEN : { < GTEQ : ">=" > }
TOKEN : { < LT : "<" > }
TOKEN : { < LTEQ : "<=" > }
TOKEN : { < ASSIGN : "=" > }
TOKEN : { < ADD : "+" > }
TOKEN : { < SUB : "-" > }
TOKEN : { < MUL : "*" > }
TOKEN : { < DIV : "/" > }
TOKEN : { < MOD : "%" > }
TOKEN : { < EXP : "^" > }
TOKEN : { < LPAR : "(" > }
TOKEN : { < RPAR : ")" > }
TOKEN : { < LCLAUSE : "{" > }
TOKEN : { < RCLAUSE : "}" > }
TOKEN : { < LBRACKET : "[" > }
TOKEN : { < RBRACKET : "]" > }
TOKEN : { < STMTBREAK : ";" > }
TOKEN : { < LISTSEP : "," > }
TOKEN : { < IDENT : <ident> > }
TOKEN : { < STRING : <string> > }
TOKEN : { < NUMBER : <num> > }
TOKEN : { < S : (<wc>)+ > }

/*
 * rule for whitespace:
 * a rule consumes all the trailing whitespace, but no
 * whitespace ahead.
 */

List<Node> parse() :
{
    Token t ;
    Node n ;
    List<Node> nodes ;
}
{
    { nodes = new ArrayList<Node>(); }
    (<S>)*
    (
        n = Statement()
        { nodes.add(n); }
    )*

    { return nodes; }
}

Type FunctionType() :
{
    Type type, type2 ;
    List<Type> params ;
}
{
    { params = new ArrayList<Type>(); }
    <LPAR>
    type = Type()
    <RPAR>
    <LPAR>
    [
        type2 = Type()
        { params.add(type2); }
        (
            ","
            (<S>)*
            type2 = Type()
            { params.add(type2); }
        )*
    ]
    <RPAR>
    { type = new FunctionType(type, params); }

    { return type; }
}

Type TupleType() :
{
    Type type, type2 ;
    List<Type> params ;
}
{
    { params = new ArrayList<Type>(); }
    <LPAR>
    [
        type2 = Type()
        { params.add(type2); }
        (
            ","
            (<S>)*
            type2 = Type()
            { params.add(type2); }
        )*
    ]
    <RPAR>
    { type = new TupleType(params); }

    { return type; }
}

Type NamedTupleType() :
{
    Token t ;
    Type type, type2 ;
    Map<String, Type> params ;
}
{
    { params = new HashMap<String, Type>(); }
    <LPAR>
    [
        t = <IDENT>
        ":"
        (<S>)*
        type2 = Type()
        { params.put(t.image, type2); }
        (
            ","
            (<S>)*
            t = <IDENT>
            ":"
            (<S>)*
            type2 = Type()
            { params.put(t.image, type2); }
        )*
    ]
    <RPAR>
    { type = new NamedTupleType(params); }

    { return type; }
}

Type Type() :
{
    Token t ;
    Type type, type2 ;
    List<Type> params ;
}
{
    (
        LOOKAHEAD ( FunctionType() )
        type = FunctionType()
        |
        LOOKAHEAD ( TupleType() )
        type = TupleType()
        |
        LOOKAHEAD ( NamedTupleType() )
        type = NamedTupleType()
        |
        (
            t = <IDENT>
            { type = new PlaceholderType(t.image); }
        )
    )
    (
        <LBRACKET>
        <RBRACKET>
        { type = new ArrayType(type); }
    )*
    (<S>)*

    { return type; }
}

Node Statement() :
{
    Token t ;
    Node n, n2 ;
}
{
    (
        LOOKAHEAD(<IFSTMT>)
            n = IfStatement()
        |
        LOOKAHEAD(<RETURNSTMT>)
            <RETURNSTMT>
            (<S>)+
            n = Expression()
            <STMTBREAK>
            (<S>)*
            { n = new ReturnStatement(n); }
        |
        LOOKAHEAD (<IDENT>(<S>)+<ASSIGN>)
            t = <IDENT>
            (<S>)+
            { n = new Variable(t.image, true); }
            <ASSIGN>
            (<S>)*
            n2 = Expression()
            { n = new AssignNode(n, n2); }
            <STMTBREAK>
            (<S>)*
        |
        LOOKAHEAD(Declaration() <ASSIGN>)
            n = Declaration()
            <ASSIGN>
            (<S>)*
            n2 = Expression()
            { n = new AssignNode(n, n2); }
            <STMTBREAK>
            (<S>)*
        |
            n = Expression()
            <STMTBREAK>
            (<S>)*
    )

    { return n; }
}

Node IfStatement() :
{
    Token t ;
    Node n, n2 ;
    List<Node> stmts;
}
{
    { stmts = new ArrayList<Node>(); }
    <IFSTMT>
    (<S>)*
    n = Expression()
    <LCLAUSE>
    (<S>)*
    (
        n2 = Statement()
        { stmts.add(n2); }
    )*
    <RCLAUSE>
    (<S>)*

     { return new IfStatement(n, stmts); }
}

Declaration Declaration() :
{
    Token t ;
    Type type ;
    String name ;
    Node value ;
    Declaration d ;
}
{
    type = Type()
    t = <IDENT>
    { name = t.image; }
    { d = new Declaration(type, name); }
    (<S>)*

    { return d; }
}

Node Tuple() :
{
    List<Node> nodes ;
    Node n ;
}
{
    { nodes = new ArrayList<Node>(); }
    <LPAR>
    (<S>)*
    n = Expression()
    { nodes.add(n); }
    (
        ","
        (<S>)*
        n = Expression()
        { nodes.add(n); }
    )*
    <RPAR>
    (<S>)*

    { return new TupleNode(nodes); }
}

Node NamedTuple() :
{
    Map<String, Node> nodes ;
    Token t ;
    Node n ;
}
{
    { nodes = new LinkedHashMap<String, Node>(); }
    <LPAR>
    (<S>)*
    t = <IDENT>
    ":"
    (<S>)*
    n = Expression()
    { nodes.put(t.image, n); }
    (
        ","
        (<S>)*
        t = <IDENT>
        ":"
        (<S>)*
        n = Expression()
        { nodes.put(t.image, n); }
    )*
    <RPAR>
    (<S>)*

    { return new NamedTupleNode(nodes); }
}

Node Array() :
{
    List<Node> nodes ;
    Node n ;
}
{
    { nodes = new ArrayList<Node>(); }
    <LBRACKET>
    (<S>)*
    n = Expression()
    { nodes.add(n); }
    (
        ","
        (<S>)*
        n = Expression()
        { nodes.add(n); }
    )*
    <RBRACKET>
    (<S>)*

    { return new ArrayNode(nodes); }
}

Node Function() :
{
    Token t ;
    Type type ;
    Node n ;
    Declaration d ;
    FunctionDecl f ;
    List<Declaration> params ;
    List<Node> statements ;
}
{
    { params = new ArrayList<Declaration>(); }
    { statements = new ArrayList<Node>(); }
    <LPAR>
    type = Type()
    <RPAR>
    <LPAR>
    [
        d = Declaration()
        { params.add(d); }
        (
            ","
            (<S>)*
            d = Declaration()
            { params.add(d); }
        )*
    ]
    <RPAR>
    (<S>)*
    <LCLAUSE>
        (<S>)*
        (
            n = Statement()
            { statements.add(n); }
        )*
    <RCLAUSE>
    (<S>)*

    {
        n = new FunctionDecl(type, params, statements);
        return n;
    }
}

Node Expression() :
{
    Token t ;
    Node n, n2 ;
}
{
    n = Expression2()
    (
            <AND>
            (<S>)*
            n2 = Expression2()
            { n = new AndNode(n, n2); }
        |
            <OR>
            (<S>)*
            n2 = Expression2()
            { n = new OrNode(n, n2); }
        |
            <XOR>
            (<S>)*
            n2 = Expression2()
            { n = new XorNode(n, n2); }
    )*

    { return n; }
}

Node Expression2() :
{
    Token t ;
    Node n, n2 ;
}
{
    n = Expression3()
    (
            <EQ>
            (<S>)*
            n2 = Expression3()
            { n = new EqualNode(n, n2); }
        |
            <NOTEQ>
            (<S>)*
            n2 = Expression3()
            { n = new NotEqualNode(n, n2); }
        |
            <LT>
            (<S>)*
            n2 = Expression3()
            { n = new LtNode(n, n2); }
        |
            <GT>
            (<S>)*
            n2 = Expression3()
            { n = new GtNode(n, n2); }
        |
            <LTEQ>
            (<S>)*
            n2 = Expression3()
            { n = new LtEqNode(n, n2); }
        |
            <GTEQ>
            (<S>)*
            n2 = Expression3()
            { n = new GtEqNode(n, n2); }
    )*

    { return n; }
}

Node Expression3() :
{
    Token t ;
    Node n, n2 ;
}
{
    (
        n = Expression4()
        (
                <ADD>
                (<S>)*
                n2 = Expression4()
                { n = new AddNode(n, n2); }
            |
                <SUB>
                (<S>)*
                n2 = Expression4()
                { n = new SubNode(n, n2); }
        )*
    )

    { return n; }
}

Node Expression4() :
{
    Token t ;
    Node n, n2 ;
}
{
    (
        n = Expression5()
        (
                <MUL>
                (<S>)*
                n2 = Expression5()
                { n = new MulNode(n, n2); }
            |
                <DIV>
                (<S>)*
                n2 = Expression5()
                { n = new DivNode(n, n2); }
            |
                <MOD>
                (<S>)*
                n2 = Expression5()
                { n = new ModNode(n, n2); }
        )*
    )

    { return n; }
}

Node Expression5() :
{
    Token t, t2 ;
    Node n, n2 ;
    List<Node> params ;
}
{
    (
        LOOKAHEAD( Function() )
            n = Function()
        |
        LOOKAHEAD( Tuple() )
            n = Tuple()
        |
        LOOKAHEAD( NamedTuple() )
            n = NamedTuple()
        |
        LOOKAHEAD( Array() )
            n = Array()
        |
        LOOKAHEAD( <IDENT> <LPAR> )
        (
            t = <IDENT>
            <LPAR>
            (<S>)*
            { params = new ArrayList<Node>(); }

            [
                n2 = Expression()
                { params.add(n2); }
                (
                    <LISTSEP>
                    (<S>)*
                    n2 = Expression()
                    { params.add(n2); }
                )*
            ]
            <RPAR>
            { n = new FunctionCall(t.image, params); }
            (<S>)*
        )
        |
            <LPAR>
            (<S>)*
            n = Expression()
            <RPAR>
            (<S>)*
        |
            t = <IDENT>
            (<S>)*
            { n = new Variable(t.image, false); }
        |
            t = <NUMBER>
            (<S>)*
            { n = new IntegerConstant(t.image); }
        |
            t = <STRING>
            (<S>)*
            { n = new StringConstant(t.image.substring(1, t.image.length()-1)); }
    )

    [
            <SUBSCRIPT>
            t = <IDENT>
            (<S>)*
            { n = new SubscriptNode(n, t.image, false); }
        |
            <LBRACKET>
            n2 = Expression()
            <RBRACKET>
            (<S>)*
            { n = new IndexNode(n, n2, false); }
    ]

    { return n; }
}
